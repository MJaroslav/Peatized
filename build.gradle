buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

repositories {
    maven {
        name "HWYLA"
        url "http://tehnut.info/maven"
    }
    maven {
        name "CB Repo"
        url "http://chickenbones.net/maven/"
    }
    maven {
        name = "JEI"
        url = "https://modmaven.k-4u.nl"
    }
    maven {
        name = "player.to"
        url = "http://maven.ic2.player.to"
    }
    maven {
        name "BuildCraft"
        url = "https://mod-buildcraft.com/maven"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"

ext.config = parseConfig(file("build.properties"))
ext.mod = parseConfig(file("mod.properties"))

version = "${config.mc_version}-${mod.version}-${mod.type}"
group = mod.group
archivesBaseName = mod.name

minecraft {
    version = "${config.mc_version}-${config.forge_version}"
    runDir = "${config.run_dir}"
    mappings = "${config.mappings}"
    replace "@VERSION@", version
    replaceIn "ModInfo.java"
    makeObfSourceJar = false
}

configurations {
    compile.extendsFrom(deobfProvided)
}

dependencies {
    runtime "mcp.mobius.waila:Hwyla:1.8.26-B41_1.12.2"
    deobfProvided "net.sengir.forestry:forestry_1.12.2:5.8.2.387:api"
    runtime "net.sengir.forestry:forestry_1.12.2:5.8.2.387"
    deobfProvided "mezz.jei:jei_1.12.2:4.14.4.264:api"
    runtime "mezz.jei:jei_1.12.2:4.14.4.264"
    deobfProvided "net.industrial-craft:industrialcraft-2:2.8.158-ex112:api"
//    runtime "net.industrial-craft:industrialcraft-2:2.8.158-ex112:dev"
    deobfProvided "com.mod-buildcraft:buildcraft-api:7.99.19"
//    runtime "com.mod-buildcraft:buildcraft:7.99.19"
}

jar {
    from configurations.deobfProvided.collect {
        it.isDirectory() ? it : zipTree(it)
    }
}

compileJava.options.encoding = "UTF-8"

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
        expand "version":project.version, "mcversion":project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

task deobfJar(type: Jar) {
    from(sourceSets.main.output)
    from configurations.deobfProvided.collect {
        it.isDirectory() ? it : zipTree(it)
    }
    classifier = "deobf"
}

task srcJar(type: Jar) {
    from(sourceSets.main.java)
    classifier = "sources"
}

task apiJar(type: Jar) {
    from(sourceSets.main.allJava)
    from(sourceSets.main.output)
    include "mjaroslav/mcmods/peatized/api/**"
    classifier = "api"
}

artifacts {
    archives srcJar, deobfJar, apiJar
}

static def parseConfig(File config) {
    if(config.exists()){
        config.withReader {
            def prop = new Properties()
            prop.load(it)
            return (new ConfigSlurper().parse(prop))
        }
    } else {
        return null
    }
}
